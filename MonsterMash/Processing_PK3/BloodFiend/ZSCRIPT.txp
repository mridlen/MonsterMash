#include "./YOMAMA.txt"
#include "./Scripts/YOMAMA.txt"
Class Bloodfiend : Demon
{
  Default
  {
    Health 250;
    PainChance 140;
    Speed 12;
    Radius 30;
    Height 56;
    Mass 450;
    Obituary "%o succumbed to a Bloodfiend's bile.";
    HitObituary "%o was mauled by a Bloodfiend.";
    SeeSound "Monster/sg2sit";
    AttackSound "Monster/sg2atk";
    PainSound "demon/Pain";
    DeathSound "monster/sg2dth";
    ActiveSound "demon/sg2act";
    Monster;
    +FloorClip
    +LongMeleeRange
    +DontHarmClass
  }

  States
  {
  Spawn:
    SAR2 AB 10 A_Look();
    Loop;
  See:
    SAR2 AABBCCDD 2 A_Chase();
    Loop;
  Melee:
    SAR2 EF 8 A_FaceTarget();
    SAR2 G 8 A_SargAttack();
    Goto See;
  Missile:
    SAR2 GF 8 A_FaceTarget();
    SAR2 E 8
    {
      A_PlaySound("Weapons/bloodf");
      A_SpawnProjectile("Bloodshot",22,0,-1,0,0);
      A_SpawnProjectile("Bloodshot",22,0, 1,0,0);
    }
    Goto See;
  Pain:
    SAR2 H 2;
    SAR2 H 2 A_Pain();
    Goto See;
  Death:
    SAR2 I 8;
    SAR2 J 8 A_Scream();
    SAR2 K 4;
    SAR2 L 4 A_NoBlocking();
    SAR2 M 4;
    SAR2 N -1;
    Stop;
  Raise:
    SAR2 NMLKJI 5;
    Goto See;
  }
}

Class Bloodshot : Actor
{
  Default
  {
    Radius 3;
    Height 3;
    Speed 20;
    Damage 2;
    RenderStyle "Translucent";
	Scale 0.8;
    Alpha 0.80;
    DeathSound "weapons/bloodx";
    Projectile;
    +ThruGhost
  }

  States
  {
  Spawn:
    FGOR A 2 Bright A_CStaffMissileSlither();
    FGOR A 0 A_SpawnItemEx("Bloodtrail",0,0,0,0,0,0,128);
    FGOR B 2 Bright A_CStaffMissileSlither();
    FGOR B 0 A_SpawnItemEx("Bloodtrail",0,0,0,0,0,0,128);
    Loop;
  Death:
    FGOR CDE 3 Bright;
    Stop;
  }
}

Class Bloodtrail : Actor
{
  Default
  {
    Radius 1;
    Height 1;
    RenderStyle "Translucent";
	Scale 0.5;
    Alpha 0.67;
    Projectile;
  }

  States
  {
  Spawn:
    TNT1 A 3;
    FGOR FGH 3 BRIGHT;
    Stop;
  }
}

Class Lurker : Bloodfiend
{
  Default
  {
    RenderStyle "Translucent";
    Obituary "%o didn't See the lurker that was shooting at him.";
    HitObituary "%o didn't See the lurker right in front of him.";
    //DropItem "BloodAmmo";
    Alpha 0.25;
  }
}
